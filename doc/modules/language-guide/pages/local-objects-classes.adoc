= ローカルオブジェクトとクラス
:proglang: Motoko
:company-id: DFINITY

{proglang} において `object` とは、ローカルのステート（`var` に束縛された変数）を、そのステートにアクセスしたり更新したりする `public` メソッドを組み込んでカプセル化したものです。

{proglang} プログラムは、他の型付け言語と同様に、ステートをカプセル化して抽象的な型を持つオブジェクトを生成できることによる利便性を享受しています。

しかしながら、可変（Mutable）なステートを含む {proglang} オブジェクトは _共有不可能（ Not shareable）_ となっています。これはセキュリティを指向するための重要な設計上の決定です。

もしもミュータブルなステートを含むオブジェクトが共有可能（Shareable）だとすると、概念的にはオブジェクトのコードを Actor 間で移動させてリモートで実行することや、リモートのロジックにステートを共有することになり、セキュリティ上のリスクとなります。（ただし、オブジェクトが純粋な record 型である場合には、ミュータブルなステートではなくなるため、共有可能です。）

このセキュリティ上必要な制限を補うために、`Actor` オブジェクトは _共有可能_ ですが、常にリモートで実行されます。
これは共有可能な {proglang} データとのみ通信します。
ローカルのオブジェクトは、自分自身とはそれほど制限されずに対話し、他のオブジェクトも含めた任意の {proglang} データをお互いのメソッドに渡すことができます。
共有不可能な点を除くと、その他のほとんどの点でローカルオブジェクト (およびクラス) は Actor オブジェクト (およびクラス) とよく似ています。

link:mutable-state{outfilesuffix}[ミュータブルなステート]では、`var` に束縛された変数や（ミュータブルな）配列の割り当てといった形で、プライベートなミュータブルステートの宣言の仕方について紹介しています。
この章では、オブジェクト指向プログラミングで簡単なオブジェクトを実装するのと同じように、ミュータブルなステートを使って簡単なオブジェクトを実装します。

この言語サポートを実行例を使って説明し、次の章へと続きます。
以下の例は、{proglang} プログラムの進化の道程を表しています。
各 _オブジェクト_ は、その重要性が高いのであれば、_（ローカル） オブジェクト_ を _Actor オブジェクト_ にリファクタリングすることで、{IC} の _Service_ へとリファクタリングできる可能性があります。

*オブジェクトクラス*。あるタスクを実行するために、関連するオブジェクト _群_ が必要になることがよくあります。
オブジェクトが似たような動作をする場合、初期のステートはカスタマイズ可能にしつつ、同じ設計図に基づいてオブジェクトを作成することは理にかなっています。
この目的のため、{proglang} は `class` 定義と呼ばれる構文構造を提供しており、同じ型で同じ実装のオブジェクトを簡単に作ることができます。
オブジェクトについて説明した後に、これらを紹介します。

*Actor クラス*。
オブジェクトクラスが _link:actors-async.adoc[Service]_（非同期動作）を公開している場合、対応する {proglang} の構造は link:actors-async.adoc[Actor クラス]であり、よく似た（ただし異なる）設計に従います。


== 例：カウンタオブジェクト

次のように、オブジェクト値である `counter` の _オブジェクト宣言_ を考えてみましょう。

[source, motoko]
....
object counter {
  var count = 0;
  public func inc() { count += 1 };
  public func read() : Nat { count };
  public func bump() : Nat {
    inc();
    read()
  };
};
....

この宣言により、`counter` という名前のオブジェクトのインスタンスが導入され、その全体の実装は上のようになります。

この例では、開発者は3つの _public_ 関数である `inc`、`read`、`bump` を、
キーワード `public` を使用してオブジェクトの本体で宣言しています。
オブジェクトの本体は、ブロック式のように宣言のリストで構成されています。

これらの3つの関数に加えて、オブジェクトは1つの（プライベートな）ミュータブル変数 `count` を持ち、現在のカウントの値（初期値は 0）を保持します。

== オブジェクト型

この `counter` オブジェクトは、次のような _オブジェクト型_ を持ち、これはフィールド型のペアを中括弧（`{` と `}`）で囲んだリストとして書かれます。

[source.no-repl, motoko]
....
{
  inc  : () -> () ;
  read : () -> Nat ;
  bump : () -> Nat ;
}
....


各フィールドの型は、識別子、コロン `:`、フィールドの内容を表す型によって構成されています。ここでは、各フィールドは関数なので、_アロー_ 型の形式（`_ -> _`）になっています。

`object` の宣言では、変数 `count` が `public` と `private` のどちらにも明示的に宣言されていません。

デフォルトでは、オブジェクトブロック内のすべての宣言は `private` となり、ここでの `count` も同様です。
結果的に、`count` の型はオブジェクトの型には現れず、その名前も存在も外部からはアクセスできません。

フィールドにアクセスできないということには強力なメリットがあります。
実装の詳細を公開しないことで、オブジェクトは _より一般的な_ 型（より少ないフィールド）を持ちます。その結果、couter オブジェクトと同じ型で実装が異なるオブジェクトと、フィールドを使用せずに交換することができます。


== 例: `byteCounter` オブジェクト

上記の点を説明するために、`counter` 宣言のバリエーションである `byteCounter` を考えてみましょう。

[source, motoko]
....
import Nat8 "mo:base/Nat8";
object byteCounter {
  var count : Nat8 = 0;
  public func inc() { count += 1 };
  public func read() : Nat { Nat8.toNat(count) };
  public func bump() : Nat { inc(); read() };
};
....

このオブジェクトは `counter` と同じ型を持っているので、型チェックの観点からは `counter` と交換可能です。

[source.no-repl, motoko]
....
{
  inc  : () -> () ;
  read : () -> Nat ;
  bump : () -> Nat ;
}
....

しかし、はじめに示した `counter` とはフィールドの実装は同じではありません。
このバージョンでは、オーバーフローしない通常の自然数 `Nat` を使用するのではなく、サイズが常に 8 ビットである自然数 (`Nat8` 型) を使用します。

このようにすることで、`inc` オペレーションがオーバーフローを起こして失敗する可能性があります。以前のバージョンでは決してオーバーフローすることはありませんでしたが、その代わりにプログラムのメモリを埋め尽くしてしまうという、別のアプリケーションエラーを引き起こす可能性があります。

いずれの実装にもある程度の複雑さがありますが、どちらも同じ型となっています。

一般的に、（オブジェクトや Service の）2つの実装間で同じ型となるようにすると、内部の実装の複雑さを隠蔽し、それを使用するアプリケーションの残りの部分から取り除きます。
ここでは、2つのオブジェクトに共通の型（`Nat`）は、数値の表現方法に関する選択を抽象化しています。
上の例は単純なものでしたが、一般的には実装の選択はより複雑で、より興味深いものになるでしょう。

== オブジェクトのサブタイピング

{proglang} におけるオブジェクトのサブタイピングの役割と使い方を説明するために、より一般的な（パブリックな操作が少ない）型で、よりシンプルなカウンタを実装してみましょう。

[source, motoko]
....
object bumpCounter {
  var c = 0; public func bump() : Nat { c += 1; c };
};
....

オブジェクト `bumpCounter` は次のようなオブジェクト型を持ち、ただ1つの操作である `bump` を公開しています。

[source.no-repl, motoko]
....
{ bump : () -> Nat }
....

この型は最も一般的な操作を公開し、特定の動作のみを許可します。
例えば、カウンタの値は増加させることしかできず、減少させたり、任意の値に設定することはできません。

システムの他の部分では、より多くの操作を備えた _より一般的でない_ バージョンを実装して使用することができます。

[source.no-repl, motoko]
....
fullCounter : {
  inc   : () -> () ;
  read  : () -> Nat ;
  bump  : () -> Nat ;
  write : Nat -> () ;
}
....

ここでは、はじめに示したものよりも一般性が低い `fullCounter` というカウンタを考えます。
このカウンタには `inc`、`read`、`bump` に加えて `write` が含まれており、呼び出し側は、現在のカウント値を `0` に戻すなど、任意の値に変更することができます。

**オブジェクトのサブタイピング**：{proglang} では、オブジェクトにはサブタイピングによって関係付けられる型があります。標準的には、_よりフィールドの多い型_ は、_より一般的でない型_ となります（_**サブ**タイプ_）。例えば、上の例で示した型は、次のような関係があるとまとめることができます。

- 最も一般的な型は以下です。

[source.no-repl, motoko]
....
{ bump : () -> Nat }
....

- 中間の一般性を持つ型は以下です。

[source.no-repl, motoko]
....
{
  inc  : () -> () ;
  read : () -> Nat ;
  bump : () -> Nat ;
}
....

- 最も一般的でない型は以下です。

[source.no-repl, motoko]
....
{
  inc  : () -> () ;
  read : () -> Nat ;
  bump : () -> Nat ;
  write : Nat -> () ;
}
....

関数が最初の型（`{ bump: () -> Nat }`) のオブジェクトを受け取ることを期待している場合、上記の型の _いずれも_ 、この（最も一般的な）型と等しいかそのサブタイプであるため、何の問題もありません。

しかし、最も一般的でない最後の型のオブジェクトを受け取ることを期待している場合、他の2つの型は十分ではありません。なぜなら、ある関数が当然アクセスできると期待している `write` 操作を、他の2つの型はどちらも持っていないためです。

== オブジェクトクラス

{proglang} では、オブジェクトはステートをカプセル化したものであり、オブジェクトの `class` は、共通の名前を持つ2つのエンティティのパッケージです。

ゼロから始まるカウンタの `class` を例に考えてみましょう。

[source#counter, motoko]
....
class Counter() {
  var c = 0;
  public func inc() : Nat {
    c += 1;
    return c;
  }
};
....

この定義の価値は、新しいカウンタとしてインスタンスを生成（_construct_）できることです。
生成したカウンタは、それぞれが独自のステートで始まり、初期値はゼロになります。

[source#cinit.include_counter, motoko]
....
let c1 = Counter();
let c2 = Counter();
....

これらはそれぞれ独立しています。

[source.include_counter_cinit, motoko]
....
let x = c1.inc();
let y = c2.inc();
(x, y)
....

オブジェクトを返す関数を書くことでも同じ結果を得ることができます。

[source, motoko]
....
func Counter() : { inc : () -> Nat } =
  object {
    var c = 0;
    public func inc() : Nat { c += 1; c }
  };
....

この _コンストラクタ関数_ の戻り値の型（オブジェクト型）に注目してください。

[source.no-repl, motoko]
....
{ inc : () -> Nat }
....

この型を例えば `Counter` と名付け、次のように型宣言に使用することができます。

[source.no-repl, motoko]
....
type Counter = { inc : () -> Nat };
....

実際、上に示した `class` キーワードの構文は、`Counter` に対するこれら 2 つの定義の略記に他なりません。2つの定義とは、オブジェクトを構築するファクトリ関数 `Counter` と、これらのオブジェクトの型 `Counter` のことです。クラスはこの利便性以上の新しい機能を提供するものではありません。

=== クラスコンストラクタ

オブジェクトクラスは、0 個以上のデータ引数と、0 個以上の型引数を持つことができるコンストラクタ関数を定義しています。

上の `Counter` の例では、それぞれ 0 個です。

型引数がある場合は、そのクラスの型とコンストラクタ関数の両方をパラメータ化します。

データ引数がある場合は、クラスのコンストラクタ関数（のみ）をパラメータ化します。

==== データ引数

カウンターを 0 以外の値で初期化したいとします。その値を `class` コンストラクタのデータ引数として与えることができます。

[source, motoko]
....
class Counter(init : Nat) {
  var c = init;
  public func inc() : Nat { c += 1; c };
};
....

このパラメータはすべてのメソッドで利用可能です。

例えば、初期値のパラメータに対して `Counter` を `reset` することができます。

[source, motoko]
....
class Counter(init : Nat) {
  var c = init;
  public func inc() : Nat { c += 1; c };
  public func reset() { c := init };
};
....

==== 型引数

カウントするためのデータを特殊な `Buffer` のようにしてカウンタに持たせたいとします。

クラスが任意の型のデータを使用したり含む場合、型関数と同様に、未知の型のための引数（_型引数_）を持ちます。

この型引数のスコープは、データ引数と同じように `class` 全体をカバーします。
そのため、クラスのメソッドはこれらの型引数を使用することができます（_再び導入_ する必要はありません）。

[source, motoko]
....
import Buffer "mo:base/Buffer";

class Counter<X>(init : Buffer.Buffer<X>) {
  var buffer = init.clone();
  public func add(x : X) : Nat { buffer.add(x); buffer.size() };
  public func reset() { buffer := init.clone() };
};
....

==== 型注釈

オプションで、クラスのコンストラクタに "戻り値の型" （生成するオブジェクトの型）の型注釈を付けることもできます。
型注釈が付与されると、{proglang} はこの型注釈がクラスの本体（オブジェクト定義）と互換性があるかどうかをチェックします。
このチェックにより、コンストラクタが生成する各オブジェクトが提供された仕様に適合することが保証されます。

例えば、先述の `Counter` に対して、より一般的な型である `Accum<X>` で型注釈します。`Accum<X>` は値を増やすことはできますが、リセットすることはできないものとします。
この注釈により、オブジェクトは `Accum<X>` 型と互換性があることが保証されます。

[source, motoko]
....
import Buffer "mo:base/Buffer";

type Accum<X> = { add : X -> Nat };

class Counter<X>(init : Buffer.Buffer<X>) : Accum<X> {
  var buffer = init.clone();
  public func add(x : X) : Nat { buffer.add(x); buffer.size() };
  public func reset() { buffer := init.clone() };
};
....

==== 全ての構文

クラスは、キーワード `class` に続けて以下を与えることで定義します。

- 定義されるコンストラクタと型の名前（たとえば `Counter`）
- オプションの型引数（省略するか、`<X>`、`<X, Y>` など）
- 引数リスト（`()` または `(init : Nat)` など）
- コンストラクタに生成されたオブジェクトに対するオプションの型注釈（省略するか、例えば `Accum<X>` など）
- クラスの "本体" はオブジェクトの定義であり、（もしあれば）型と値の引数によってパラメータ化されます。

`public` とマークされたクラス本体の構成要素は、生成されるオブジェクトの型に寄与し、これらの型は（オプションの）型注釈と比較されます。

=====  他の例: `Bits`

他の例として、自然数 `Nat` のビット移動のタスクを考えてみましょう。例えば、以下のように定義することができます。

[source, motoko]
....
class Bits(n : Nat) {
  var state = n;
  public func next() : ?Bool {
    if (state == 0) { return null };
    let prev = state;
    state /= 2;
    ?(state * 2 != prev)
  }
}
....

上記のクラス定義は、構造的な型シノニムとファクトリ関数をどちらも `Bits` という名前で同時に定義することと同義です。

[source, motoko]
....
type Bits = {next : () -> ?Bool}
let Bits : Nat -> Bits =
func Bits(n : Nat) : Bits = object {
  // クラス本体
};
....

== 構造的サブタイピング

{proglang} におけるオブジェクトのサブタイピングは _構造的サブタイピング_ を使用しており、_公称型サブタイピング_ ではありません。

公称型サブタイピングでは、2 つの型が等価かどうかは（プロジェクトや時間を超えて）一貫した、グローバルにユニークな型名の選択に依存することを思い出してください。

{proglang} では、2 つの型の等価性の問題は、名前ではなく _構造_ に基づきます。

構造的サブタイピングによって、クラスの型に名前を付けることによって便利な省略形として使うことができます。

しかし、型付けの目的において重要なのは、対応するオブジェクト型の _構造_ です。名前が違っても、同等の定義を持つ 2 つのクラスは型互換性のあるオブジェクトを生成します。

クラス宣言の中で型注釈が指定された場合、その適合性がチェックされます。オブジェクトの型は型注釈のサブタイプでなければなりません。ただし、型注釈がオブジェクト型の適切なスーパータイプを記述しているだけであっても、型注釈はそのクラスの型に影響しません。

形式的には、{proglang} のサブタイプの関係は、オブジェクト型だけでなく、すべての型に拡張されます。

ほとんどの場合は標準的なものであり、従来のプログラミング言語理論（特に _構造的サブタイピング_）に従っています。

新しいプログラマにとって、{proglang} の他の注目すべき事柄は配列、オプション、バリアント、数値型の相互関係です。

////
// TODO - Move examples into doc/modules/language-guide/examples
// Use this syntax to include the files here:
// include::example$file-name.mo[]
= Local objects and classes
:proglang: Motoko
:company-id: DFINITY

In {proglang}, an `object` may encapsulate local state (`var`-bound variables) by packaging this state with `public` methods that access and update it.

As in other typed languages, {proglang} programs benefit from the ability to encapsulate state as objects with abstract types.

However, {proglang} objects that include mutable state are _not shareable_, and this is a critical security-oriented design decision.

If they were shareable, that would mean either conceptually moving a mobile object's code among actors and executing it remotely, a security risk, or sharing state with remote logic, another security risk.  (Notably, as a subcase, objects may be pure records and those _are_ shareable, since they are free from mutable state.)

To compensate for this necessary limitation, `actor` objects _are shareable_, but always execute remotely.
They communicate with shareable {proglang} data only.
Local objects interact in less restricted ways with themselves, and can pass any {proglang} data to each other's methods, including other objects.
In most other ways, local objects (and classes) are non-shareable counterparts to actor objects (and classes).

The link:mutable-state{outfilesuffix}[Mutable state] introduced declarations of private mutable state, in the form of `var`-bound variables and (mutable) array allocation.
In this chapter, we use mutable state to implement simple objects, much like how we would implement simple objects in object-oriented programming.

We illustrate this support via a running example, which continues in the next chapter.
The following example illustrates a general evolution path for {proglang} programs.
Each _object_, if important enough, has the potential to be refactored into an Internet _service_, by refactoring this _(local) object_ into an _actor object_.

*Object classes*. Frequently, one needs _a family_ of related objects to perform a task.
When objects exhibit similar behavior, it makes sense to fabricate them according to the same blueprint,
but with customizable initial state.
To this end, {proglang} provides a syntactical construct, called a `class` definition,
which simplifies building objects of the same type and implementation.
We introduce these after discussing objects.

*Actor classes*.
When an object class exposes a _link:actors-async.adoc[service]_ (asynchronous behavior), the corresponding {proglang} construct is an link:actors-async.adoc[actor class], which follows a similar (but distinct) design.


== Example: The `counter` object

Consider the following _object declaration_ of the object value `counter`:

[source, motoko]
....
object counter {
  var count = 0;
  public func inc() { count += 1 };
  public func read() : Nat { count };
  public func bump() : Nat {
    inc();
    read()
  };
};
....

This declaration introduces a single object instance named `counter`, whose entire implementation is given above.

In this example, the developer exposes three _public_ functions `inc`, `read` and `bump` using keyword `public` to declare each in the object body.
The body of the object, like a block expression, consists of a list of declarations.

In addition to these three functions, the object has one (private) mutable variable `count`, which holds the current count, initially zero.

== Object types

This object `counter` has the following _object type_, written as a list of field-type pairs, enclosed in braces (`{` and `}`):

[source.no-repl, motoko]
....
{
  inc  : () -> () ;
  read : () -> Nat ;
  bump : () -> Nat ;
}
....

Each field type consists of an identifier, a colon `:`, and a type for the field content.  Here, each field is a function, and thus has an _arrow_ type form (`_ -> _`).

In the declaration of `object`, the variable `count` was explicitly declared neither as `public` nor as `private`.

By default, all declarations in an object block are `private`, as is `count` here.
Consequently, the type for `count` does not appear in the type of the object, _and_ its name and presence are both inaccessible from the outside.

The inaccessibility of this field comes with a powerful benefit:
By not exposing this implementation detail, the object has a _more general_ type (fewer fields), and as a result, is interchangeable with objects that implement the same counter object type differently, without using such a field.

== Example: The `byteCounter` object

To illustrate the point just above, consider this variation of the `counter` declaration above, of `byteCounter`:

[source, motoko]
....
import Nat8 "mo:base/Nat8";
object byteCounter {
  var count : Nat8 = 0;
  public func inc() { count += 1 };
  public func read() : Nat { Nat8.toNat(count) };
  public func bump() : Nat { inc(); read() };
};
....

This object has the same type as the previous one, and thus from the standpoint of type checking, this object is interchangeable with the prior one:

[source.no-repl, motoko]
....
{
  inc  : () -> () ;
  read : () -> Nat ;
  bump : () -> Nat ;
}
....

Unlike the first version, however, this version does not use the same implementation of the counter field.
Rather than use an ordinary natural `Nat` that never overflows, but may also grow without bound, this version uses a byte-sized natural number (type `Nat8`) whose size is always eight bits.

As such, the `inc` operation may fail with an overflow for this object, but never the prior one, which may instead (eventually) fill the program's memory, a different kind of application failure.

Neither implementation of a counter comes without some complexity, but in this case, they share a common type.

In general, a common type shared among two implementations (of an object or service) affords the potential for the internal implementation complexity to be factored away from the rest of the application that uses it.
Here, the common type abstracts over the simple choice of a number's representation.
In general, the implementation choices would each be more complex, and more interesting.

== Object subtyping

To illustrate the role and use of object subtyping in {proglang}, consider implementing a simpler counter with a more general type (fewer public operations):

[source, motoko]
....
object bumpCounter {
  var c = 0; public func bump() : Nat { c += 1; c };
};
....

The object `bumpCounter` has the following object type, exposing exactly one operation, `bump`:

[source.no-repl, motoko]
....
{ bump : () -> Nat }
....

This type exposes the most common operation, and one that only permits certain behavior.
For instance, the counter can only ever increase, and can never decrease or be set to an arbitrary value.

In other parts of a system, we may in fact implement and use a _less general_ version, with _more_ operations:

[source.no-repl, motoko]
....
fullCounter : {
  inc   : () -> () ;
  read  : () -> Nat ;
  bump  : () -> Nat ;
  write : Nat -> () ;
}
....

Here, we consider a counter named `fullCounter` with a less general type than any given above.
In addition to `inc`, `read` and `bump`, it additionally includes `write`, which permits the caller to change the current count value to an arbitrary one, such as back to `0`.

**Object subtyping.** In {proglang}, objects have types that may
  relate by subtyping, as the various types of counters do above.  As
  is standard, types with _more fields_ are _less general_ (are _**sub**types_
  of) types with _fewer fields_.  For instance, we can summarize the
  types given in the examples above as being related in the following
  subtyping order:

- Most general:

[source.no-repl, motoko]
....
{ bump : () -> Nat }
....

- Middle generality:

[source.no-repl, motoko]
....
{
  inc  : () -> () ;
  read : () -> Nat ;
  bump : () -> Nat ;
}
....

- Least generality:

[source.no-repl, motoko]
....
{
  inc  : () -> () ;
  read : () -> Nat ;
  bump : () -> Nat ;
  write : Nat -> () ;
}
....

If a function expects to receive an object of the first type (`{ bump: () -> Nat }`), _any_ of the types given above will suffice, since they are each equal to, or a subtype of, this (most general) type.

However, if a function expects to receive an object of the last, least general type, the other two will _not_ suffice, since they each lack the needed `write` operation, to which this function rightfully expects to have access.

== Object classes

In {proglang}, an object encapsulates state, and an object `class` is
a package of two entities that share a common name.

Consider this example `class` for counters that start at zero:

[source#counter, motoko]
....
class Counter() {
  var c = 0;
  public func inc() : Nat {
    c += 1;
    return c;
  }
};
....

The value of this definition is that we can _construct_ new counters,
each starting with their own unique state, initially at zero:

[source#cinit.include_counter, motoko]
....
let c1 = Counter();
let c2 = Counter();
....

Each is independent:

[source.include_counter_cinit, motoko]
....
let x = c1.inc();
let y = c2.inc();
(x, y)
....

We could achieve the same results by writing a function that returns an object:

[source, motoko]
....
func Counter() : { inc : () -> Nat } =
  object {
    var c = 0;
    public func inc() : Nat { c += 1; c }
  };
....

Notice the return type of this _constructor function_ (an object type):

[source.no-repl, motoko]
....
{ inc : () -> Nat }
....

We may want to name this type, for example, `Counter`, as follows, for use in further type declarations:

[source.no-repl, motoko]
....
type Counter = { inc : () -> Nat };
....

In fact, the `class` keyword syntax shown above is nothing but a shorthand for these two definitions for `Counter`: a factory function `Counter` that constructs objects, and the type `Counter` of these objects. Classes do not provide any new functionality beyond this convenience.

=== Class constructor

An object class defines a constructor function that may carry zero or more data arguments and zero or more type arguments.

The `Counter` example above has zero of each.

The type arguments, if any, parameterize both the type and the constructor function for the class.

The data arguments, if any, parameterize (only) the constructor function for the class.

==== Data arguments

Suppose we want to initialize the counter with some non-zero value.  We can supply that value as a data argument to the `class` constructor:

[source, motoko]
....
class Counter(init : Nat) {
  var c = init;
  public func inc() : Nat { c += 1; c };
};
....

This parameter is available to all methods.

For instance, we can `reset` the `Counter` to its initial value, a parameter:

[source, motoko]
....
class Counter(init : Nat) {
  var c = init;
  public func inc() : Nat { c += 1; c };
  public func reset() { c := init };
};
....

==== Type arguments

Suppose we want the counter to actually carry data that it counts (like a specialized `Buffer`).

When classes use or contain data of arbitrary type, they carry a type
argument (or equivalently, _type parameter_) for that unknown type, just as with functions.

The scope of this type parameter covers the entire `class`, just as with data parameters.
As such, the methods of the class can use (and _need not re-introduce_) these type parameters.

[source, motoko]
....
import Buffer "mo:base/Buffer";

class Counter<X>(init : Buffer.Buffer<X>) {
  var buffer = init.clone();
  public func add(x : X) : Nat { buffer.add(x); buffer.size() };
  public func reset() { buffer := init.clone() };
};
....

==== Type annotation

Optionally, the class constructor may also carry a type annotation for its "return type" (the type of objects that it produces).
When supplied, {proglang} checks that this type annotation is compatible with the body of the class (an object definition).
This check ensures that each object produced by the constructor meets the supplied specification.

For example, we repeat the `Counter` as a buffer, and annotate it with
a more general type `Accum<X>` that permits adding, but not resetting
the counter. This annotation ensures that the objects are compatible
with the type `Accum<X>`.

[source, motoko]
....
import Buffer "mo:base/Buffer";

type Accum<X> = { add : X -> Nat };

class Counter<X>(init : Buffer.Buffer<X>) : Accum<X> {
  var buffer = init.clone();
  public func add(x : X) : Nat { buffer.add(x); buffer.size() };
  public func reset() { buffer := init.clone() };
};
....

==== Full syntax

In full, classes are defined by the keyword `class`, followed by:
 - a name for the constructor and type being defined (for example, `Counter`)
 - optional type arguments (for example, omitted, or `<X>`, or `<X, Y>`)
 - an argument list (for example, `()`, or `(init : Nat)`, etc.)
 - an optional type annotation for the constructed objects (for example, omitted, or `Accum<X>`),
 - the class "body" is an object definition, parameterized by the type and value arguments (if any).

The constituents of the body marked `public` contribute to the resulting objects' type and these types compared against the (optional) annotation, if given.

=====  Another example: `Bits`

As another example, let's consider the task of walking the bits of a `Nat`ural number. For this example, we could define the following:

[source, motoko]
....
class Bits(n : Nat) {
  var state = n;
  public func next() : ?Bool {
    if (state == 0) { return null };
    let prev = state;
    state /= 2;
    ?(state * 2 != prev)
  }
}
....

The above class definition is equivalent to the simultaneous definition of a structural type synonym and a factory function, both named `Bits`:

[source, motoko]
....
type Bits = {next : () -> ?Bool}
let Bits : Nat -> Bits =
func Bits(n : Nat) : Bits = object {
  // class body
};
....

== Structural subtyping

Object subtyping in {proglang} uses _structural subtyping_, not _nominal subtyping_.

Recall that in nominal typing, the question of two types equality depends on choosing consistent, globally-unique type names (across projects and time).

In {proglang}, the question of two types' equality is based on their _structure_, not their names.

Due to structural typing, naming the class type provides a convenient abbreviation.

For typing purposes, however, all that matters is the _structure_ of the corresponding object type: two classes with different names but equivalent definitions produce type-compatible objects.

When the optional type annotation is supplied in a class declaration, conformance is checked: the object type must be a subtype of the annotation. The annotation does not affect the type of the class, however, even if it only describes a proper super-type of the object type.

Formally, subtyping relationships in {proglang} extend to all types, not just object types.

Most cases are standard, and follow conventional programming language theory (for _structural_ subtyping, specifically).

Other notable cases in {proglang} for new programmers include
array, options, variants and number type inter-relationships.
////